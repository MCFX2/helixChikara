class PlayerShooting : ZilchComponent
{
  //keys
  [Property] var ShootKey : Keys = Keys.Space;
  [Property] var AnimeShipKey : Keys = Keys.One;
  //[Property] var MachoCheeseShipKey : Keys = Keys.Two;
  //RIP Macho Cheese
  [Property] var ReaganShipKey : Keys = Keys.Two;
  [Property] var ScoutShipKey : Keys = Keys.Three;
  
  //bullet physics
  [Property] var BulletSpawnOffset = Real2(1.5, 0); //Bullet creation position relative to ship X, Y
  [Property] var BulletSpeedAnime = 5;
  [Property] var BulletFireDelayAnime = 6; //number of logic ticks between reloads
  [Property] var BulletSpreadAnime = 0.02; //ratio of bullet spread. 0 = laser, 1 = 180 degree firing range
  
  [Property] var MachoBombFireDelay = 240;
  [Property] var MachoBombVelocity = 5;
  
  [Property] var RandalFireDelay = 4;
  [Property] var RandalFireSpeed = 8;  
  [Property] var WaveController : CogPath;
  [Property] var HUDCharacterIndicator : CogPath;
  
  var TimeSinceLastBullet = 0;
  var CurrentShip : Ship = Ship.Anime;
  
  var Rand : Random = Random();
    
  function Initialize(init : CogInitializer)
  {
    Zero.Connect(this.Space, Events.LogicUpdate, this.OnLogicUpdate);
  }
  
  function Shoot(){
    if(this.CurrentShip == Ship.Anime){
      if(this.TimeSinceLastBullet >= this.BulletFireDelayAnime){
        var currentBullet : Cog = this.Space.CreateAtPosition(Archetype.AnimeBullet, this.Owner.Transform.Translation + Real3(this.BulletSpawnOffset, 0));
        //Y=Speed^2 * sin(ang)
        var spreadAngle = this.Rand.Range(-90, 90) * this.BulletSpreadAnime;
        var velY = Math.Pow(this.BulletSpeedAnime, 2) * Math.Sin(Math.ToRadians(spreadAngle));
        var velX = Math.Pow(this.BulletSpeedAnime, 2) * Math.Cos(Math.ToRadians(spreadAngle));
        currentBullet.RigidBody.Velocity = Real3(velX, velY, 0);
        //Note that bullets are intentionally NOT made children of Screen, so if the level changes direction bullets don't follow. Realism!
        this.TimeSinceLastBullet = 0;
      }
    }
    else if (this.CurrentShip == Ship.Reagan){
      if(this.TimeSinceLastBullet >= this.MachoBombFireDelay){
        var currentBullet = this.Space.CreateAtPosition(Archetype.MachoBomb, this.Owner.Transform.Translation + Real3(this.BulletSpawnOffset, 0));
        currentBullet.RigidBody.Velocity = Real3(this.MachoBombVelocity, 0, 0);
        this.TimeSinceLastBullet = 0;
      }
    }
    else if (this.CurrentShip == Ship.Scout){
      if(this.TimeSinceLastBullet >= this.RandalFireDelay){
        var currentBullet = this.Space.CreateAtPosition(Archetype.RandallBullet, this.Owner.Transform.Translation + Real3(this.BulletSpawnOffset, 0));
        //if(this.WaveController.WaveSpawner.CurrentEnemies == Array[Cog](){}) return;
        var nearestEnemyPos = Math.Distance(this.WaveController.WaveSpawner.CurrentEnemies[0].Transform.Translation, this.Owner.Transform.Translation);
        var nearestEnemy = this.WaveController.WaveSpawner.CurrentEnemies[0];
        
        foreach(var enemy in this.WaveController.WaveSpawner.CurrentEnemies){
          if(Math.Distance(this.Owner.Transform.Translation, enemy.Transform.Translation) < nearestEnemyPos){
            nearestEnemy = enemy;
          }
        }
        var bulletPos = this.Owner.Transform.Translation + Real3(this.BulletSpawnOffset, 0);
        var targetPos = nearestEnemy.Transform.Translation;
        
        //ripped from basic enemy bullet script
        var x = Math.Abs(targetPos.X - bulletPos.X);
        var y = Math.Abs(targetPos.Y - bulletPos.Y);
        
        var xdirection = 0;
        if(targetPos.X > bulletPos.X) { xdirection = 1; }
        else { xdirection = -1; }
        var ydirection = 0;
        if(targetPos.Y > bulletPos.Y) { ydirection = 1; }
        else { ydirection = -1; }
        var dx : Real = y/x;
        var newVel = Math.Normalize(Real2(xdirection, dx * ydirection));
        currentBullet.RigidBody.Velocity = Real3(newVel * this.RandalFireSpeed, 0);
        currentBullet.Orientation.LookAtPoint(targetPos);
        //currentBullet.RigidBody.Velocity 
        this.TimeSinceLastBullet = 0;
      }
    }
  }
  
  function ChangeShip(newShip : Ship){
    this.CurrentShip = newShip;
    //do other stuff we do when switching here
  }
  
  
  function CheckKeyboard() { //used for any key inputs that do NOT DIRECTLY affect acceleration
    if(Zero.Keyboard.KeyIsDown(this.ShootKey) && this.Owner.PlayerController.ControlType == ControlTypes.Keyboard){
      this.Shoot();
    }
    if(Zero.Keyboard.KeyIsDown(this.AnimeShipKey)){//TODO. make a single ship key and use WAD/mouse to select a ship to replace from a radial menu
      this.ChangeShip(Ship.Anime);
      this.HUDCharacterIndicator.CurrentPlayerIndicator.UpdateShipName(Ship.Anime);
    }
    /*if(Zero.Keyboard.KeyIsDown(this.MachoCheeseShipKey)){
      this.ChangeShip(Ship.MachoCheese);
      this.HUDCharacterIndicator.CurrentPlayerIndicator.UpdateShipName(Ship.MachoCheese);
    }*/
    
    //RIP Macho Cheese
    if(Zero.Keyboard.KeyIsDown(this.ReaganShipKey)){
      this.ChangeShip(Ship.Reagan);
      this.HUDCharacterIndicator.CurrentPlayerIndicator.UpdateShipName(Ship.Reagan);
    }
    if(Zero.Keyboard.KeyIsDown(this.ScoutShipKey)){
      this.ChangeShip(Ship.Scout);
      this.HUDCharacterIndicator.CurrentPlayerIndicator.UpdateShipName(Ship.Scout);
    }
    
  }
  
  function CheckMouse() {
    if(Zero.Mouse.IsButtonDown(MouseButtons.Left) && this.Owner.PlayerController.ControlType == ControlTypes.Mouse) {
        this.Shoot();
    }
  }

  function OnLogicUpdate(event : UpdateEvent) {
    this.CheckMouse();
    this.CheckKeyboard();
    ++this.TimeSinceLastBullet;
  }
  
}
